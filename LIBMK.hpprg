@ This auxiliary program builds the library from the source "EXTPK.hpdir" using
@ only the calculator. It can also be used in conjunction with the applications
@ HPUserEdit or EMU48.

@ %NAME%=LIBMK

´
  @ Ensure we're starting in the HOME directory.
  HOME

  @ Attempt to load "EXTPK.hpdir" from the HOME directory.
  @ If it's a string (type 2), assume it's a serialized directory,
  @ convert it to a directory object, and store it as "MYLIB".
  :0:EXTPK.hpdir EVAL DUP TYPE 2 == IF THEN
    STRç
    'MYLIB' PGDIR
    'MYLIB' STO
  ELSE
    DROP
      @ Attempt to load the source code from the stack as a string:
      @ Check if the stack is not empty.
      DEPTH 0 == NOT IF THEN
        @ Check if there is a string (type 2) on stack.
        DUP TYPE 2 == IF THEN
          @ Convert the string to an object and check
          @ if it is a directory object (type 15).
          DUP STRç TYPE 15 == IF THEN
            STRç
            'MYLIB' PGDIR
            'MYLIB' STO
          END
        END
      END
  END

  @ Check if the "MYLIB" directory exists and validate the library ID.
  MYLIB PATH SIZE 2 == IF THEN
    $ROMID 1723 == NOT IF THEN
      "The library in 'MYLIB' directory does not have the expected ID: 1723!"
      DOERR
    END
  ELSE
    DROP
    "The directory 'MYLIB' was not found!" DOERR
  END

  @ Remove the library from port 0 (IRAM), if present.
  :0:1723 DUP DETACH PURGE

  @ Remove the library from port port 1 (ERAM), if present.
  :1:1723 DUP DETACH PURGE

  @ Remove the library from port port 2 (FLASH), if present.
  :2:1723 DUP DETACH PURGE

  @ Attach the development library.
  256 ATTACH

  @ Create the library object from the directory.
  CRLIB

  @ Store the compiled library into port 0 (IRAM).
  0 STO

  @ Attach the final version of the library for use.
  :0:1723 ATTACH
ª
